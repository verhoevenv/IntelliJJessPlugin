{
    parserClass="com.github.verhoevenv.intellij.jess.parser.JessParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Jess"
    psiImplClassSuffix="Impl"
    psiPackage="com.github.verhoevenv.intellij.jess.psi"
    psiImplPackage="com.github.verhoevenv.intellij.jess.psi.impl"

    elementTypeHolderClass="com.github.verhoevenv.intellij.jess.psi.JessTypes"
    elementTypeClass="com.github.verhoevenv.intellij.jess.psi.JessElementType"
    tokenTypeClass="com.github.verhoevenv.intellij.jess.psi.JessTokenType"

  tokens=[
    COMMENT = "regexp:;[^\r\n]*"
    SYMBOL = "regexp:[a-zA-Z$*=+/<>_?#.-][a-zA-Z0-9$*=+/<>_?#.-]*"
    STRING_LITERAL = "regexp:(\"([^\"\\]|\\.)*\")"
    NUMBER_LITERAL='regexp:\d+(\.\d*)?'
    TRUE = "TRUE"
    FALSE = "FALSE"
    NIL = "nil"
    LP = "("
    RP = ")"
    LS = "{"
    RS = "}"
    DEFRULE = "defrule"
    DEFMODULE = "defmodule"
    DEFTEMPLATE = "deftemplate"
    DEFFACTS = "deffacts"
    DEFCLASS = "defclass"
    DEFFUNCTION = "deffunction"
    SLOT = "slot"
    DECLARE = "declare"
    ARROW = "=>"
    AMP = "&"
    PIPE = "|"
    COLON = ":"
    TILDE = "~"
    EXCLAMATION = "!"
    ASSIGN = "<-"
  ]

}

jessFile ::= form*
form ::= (rule|construct|resolvable|COMMENT)

rule ::= LP DEFRULE rulehead ARROW rulebody RP

rulehead ::= rulename ruledeclarationlist? ruleconditional*
rulename ::= modulequalification? nonqualifiedrulename
ruledeclarationlist ::= LP DECLARE ruledeclaration+ RP
ruledeclaration ::= LP (jesssymbol|literal)+ RP

private ruleconditional ::= rulefactassignment | ruleconditionlist
rulefactassignment ::= variable ASSIGN ruleconditionlist

ruleconditionlist ::= LP conditionhead conditionargument* RP
conditionhead ::= jesssymbol
private conditionargument ::= (ruleconditionlist|extendedsymbolic|newstylematching)
private extendedsymbolic ::= (SYMBOL|AMP|COLON|PIPE|TILDE|EXCLAMATION|literal|resolvable)
newstylematching ::= LS extendedsymbolic+ RS

rulebody ::= ruleaction*
ruleaction ::= resolvable

construct ::= (moduleconstruct|templateconstruct|factsconstruct|classconstruct|functionconstruct)

moduleconstruct ::= LP DEFMODULE jesssymbol+ RP

templateconstruct ::= LP DEFTEMPLATE factidentifier templatedeclarelist? templateslot* RP
factidentifier ::= modulequalification? factname
templatedeclarelist ::= LP DECLARE templatedeclaration* RP
templatedeclaration ::= LP (jesssymbol|literal)+ RP
templateslot ::= LP SLOT slotname RP
slotname ::= jesssymbol

factsconstruct ::= LP DEFFACTS factdbname fact* RP
factdbname ::= jesssymbol
fact ::= LP factname factslot* RP
factslot ::= LP slotname literal RP

classconstruct ::= LP DEFCLASS factidentifier javaclass RP
javaclass ::= jesssymbol

functionconstruct ::= LP DEFFUNCTION functionname functionargumentlist resolvable* RP
functionargumentlist ::= LP variable* RP

modulequalification ::= modulename moduleseperator
modulename ::= jesssymbol
nonqualifiedrulename ::= jesssymbol

booleanliteral ::= (TRUE | FALSE)
literal ::= STRING_LITERAL | booleanliteral | NUMBER_LITERAL | NIL
variable ::= jesssymbol
factname ::= jesssymbol

private jesssymbol ::= SYMBOL
moduleseperator ::= COLON COLON

resolvable ::= factdefinition | functioncall | methodcall | (LP resolvable RP)
factdefinition ::= LP factidentifier functionargument* RP
methodcall ::= LP resolvable functionargument* RP
functioncall ::= LP functionname functionargument* RP
functionname ::= jesssymbol
functionargument ::= (literal|variable|resolvable)